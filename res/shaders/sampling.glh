

vec2 CalcParallaxTexCoords(sampler2D dispMap, mat3 tbnMatrix, vec3 directionToEye, vec2 texCoords, float scale, float bias)
{
	return texCoords.xy + ((tbnMatrix * directionToEye) *((texture2D(dispMap, texCoords.xy).r) * scale + bias)).xy * vec2(-1,1);/// (tbnMatrix * directionToEye).z ;
}


float SampleShadowMap(sampler2D shadowMapref, vec2 coords, float compare)
{
	return step(compare, texture2D(shadowMapref, coords.xy).r);
}

float SampleShadowMapLinear(sampler2D shadowMapref, vec2 coords, float compare, vec2 texelSize)
{
	vec2 pixelPos = coords/texelSize + vec2(0.5);
	vec2 fracPart = fract(pixelPos);
	vec2 startTexel = (pixelPos - fracPart) * texelSize;
	
	float blTexel = SampleShadowMap(shadowMapref, startTexel, compare);
	float brTexel = SampleShadowMap(shadowMapref, startTexel + vec2(texelSize.x, 0.0), compare);
	float tlTexel = SampleShadowMap(shadowMapref, startTexel + vec2(0.0, texelSize.y), compare);
	float trTexel = SampleShadowMap(shadowMapref, startTexel + texelSize, compare);
	
	float mixA = mix(blTexel, tlTexel, fracPart.y);
	float mixB = mix(brTexel, trTexel, fracPart.y);
	return mix(mixA, mixB, fracPart.x);
}

float SampleShadowMapPCF(sampler2D shadowMapref, vec2 coords, float compare, vec2 texelSize)
{
	const float NUM_SAMPLES = 3.0;
	const float SAMPLES_START = (NUM_SAMPLES - 1.0)/2.0;
	const float NUM_SAMPLES_SQUARED = NUM_SAMPLES * NUM_SAMPLES;

	float result = 0.0;
	for(float y = -SAMPLES_START; y <= SAMPLES_START; y += 1.0){
		for(float x = -SAMPLES_START; x <= SAMPLES_START; x += 1.0){
			vec2 coordsOffset = vec2(x,y) * texelSize;
			result += SampleShadowMapLinear(shadowMapref, coords + coordsOffset, compare, texelSize);
		}
	}
	
	return result/NUM_SAMPLES_SQUARED;
}

float linstep(float low, float high, float v)
{
	return clamp((v-low)/(high-low), 0.0, 1.0);
}

float SampleVarianceShadowMap(sampler2D shadowMap, vec2 coords, float compare, float varianceMin, float lightBleedReductionAmount)
{
	vec2 moments = texture2D(shadowMap, coords.xy).xy;
	float p = step(compare, moments.x);
	float variance = max(moments.y - moments.x * moments.x, varianceMin);
	
	float d = compare - moments.x;
	float pMax = linstep(lightBleedReductionAmount, 1.0, variance / (variance + d*d));
	
	return min(max(p, pMax),1.0);
	
	//return step(compare, texture2D(shadowMap, coords.xy).r);
}